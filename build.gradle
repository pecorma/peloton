buildscript {
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.0")
    }
}

plugins {
    id 'com.android.application' version '7.3.0-beta05' apply false
    id 'com.android.library' version '7.3.0-beta05' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.0' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.7.0' apply false
}

ext {
    compileSdkVersion = 32
    minSdkVersion = 23
    targetSdkVersion = 32
    jvmTarget = '1.8'

    versions = [
            appcompat              : '1.4.2',
            constraintlayout       : '2.1.4',
            corektx                : '1.9.0-alpha05',
            coroutines             : '1.6.1',
            lifecycleruntime       : '2.5.0',
            material               : '1.6.1',
            okhttp                 : '4.9.3',
            retrofit               : '2.9.0',
            serialization          : '1.3.2',
            serializationConverter : '0.8.0',
    ]
}

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        def isAndroidApp = "com.android.build.gradle.AppPlugin" == plugin.class.name
        def isAndroidModule = "com.android.build.gradle.LibraryPlugin" == plugin.class.name

        if (isAndroidApp || isAndroidModule) {
            project.android {
                buildFeatures.viewBinding true

                compileSdkVersion rootProject.ext.compileSdkVersion
                defaultConfig.minSdkVersion rootProject.ext.minSdkVersion
                defaultConfig.targetSdkVersion rootProject.ext.targetSdkVersion

                defaultConfig.vectorDrawables.useSupportLibrary = true

                compileOptions.sourceCompatibility JavaVersion.VERSION_1_8
                compileOptions.targetCompatibility JavaVersion.VERSION_1_8

                packagingOptions {
                    exclude "META-INF/licenses/**"
                    exclude "META-INF/AL2.0"
                    exclude "META-INF/LGPL2.1"
                }
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
            freeCompilerArgs += '-opt-in=kotlinx.serialization.ExperimentalSerializationApi'
            freeCompilerArgs += '-opt-in=kotlinx.coroutines.FlowPreview'
            freeCompilerArgs += '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
        }
    }
}